TITLE JOB002 - JOB 2 FOR ALL PDP-10'S
;TRU CONTROL JOB

HISEG		;ALL GOES IN HIGH SEGMENT

;VERSION 10 - IF STOP AND START TIME SAME, DON'T CONTROL

;REQUIRES OP WF WC SY JL ST LICENSE

EXTERN ERROR, BL1LST, BLOCK1
EXTERN BLOCK2, THISYS

INTERN PAMJOB

;AC'S
F=0
A=1
B=2
C=3
D=4
E=5
N=6
N1=7
N2=10
N3=11
INDEX=12
STRDEX=13	;INDEX FOR STRUCTURES
STR=14
SIZENT=15
JOBN=16
P=17

;FLAGS IN RIGHT HALF OF F
DUREST==1	;THIS IS DURING RESTRICT TIME
ONESTR==2	;SET CREDIT IN DSKB ONLY
ADDIT==4	;ADD TRU'S INSTEAD OF SETTING THEM

;I/O CHANNELS
DSK==1
UFD==2

JOB==2
JBTSTS==0
JACCT==1

;VARIABLES
STRTRU=BLOCK2
STPTRU=BLOCK2+1
CLRTIM=BLOCK2+2
OVRIDE=BLOCK2+3
FIRSTR=BLOCK2+4
BNHERE=BLOCK2+5
TODAY=BLOCK2+6
DATLOC=BLOCK2+7

;RETURNS IN RIGHT HALF OF B FOR ERROR ROUTINE
GOBAK==1
NXTJ==0
NORET==-1

DEFINE MAKERR (ERRN, JN, SN, RET)
<XLIST
	SKIPA
	JRST .+6
	HRLZI A,ERRN
	HRRI A,JN
	HRLZI B,SN
	HRRI B,RET
	PUSHJ P,ERROR
LIST>


PAMJOB:	SETZB A,F
	SETUWP A,
	JFCL
	DATE A,
	MOVEM A,TODAY
	INIT DSK,17
	SIXBIT /DSK/
	0
INIERR:	MAKERR 2,JOB,@THISYS,NORET
	PUSHJ P,LOKCTL	;LOOKUP AND READ CONTROL FILE
	CLOSE DSK,
	SKIPE BNHERE	;FIRST RUN?
	JRST .+3
	AOS BNHERE	;YES
	PUSHJ P,CLROVR	;CLEAR OVERRIDE IF VERY FIRST RUN
	PUSHJ P,SETFLG	;SET FLAG FOR RESTRICT,NON-RESTR. TIME
	PUSHJ P,CHKCLR
AGN:	SKIPN C,OVRIDE
	JRST NOVRID
	JUMPL C,.+4	;OVERRIDE IS SET,WAS IT SET DURING RESTR TIME?
	TRNE F,DUREST	;YES, IS THIS RESTR TIME?
	JRST RET	;YES, SO RETURN
	JRST [PUSHJ P,CLROVR	;NO, CLEAR OVERRIDE AND CHECK AGAIN
		JRST AGN]
	TRNE F,DUREST	;OVERRIDE WAS NOT SET DURING RESTR TIME
	JRST [PUSHJ P,CLROVR	;AND THIS IS NOT RESTRICT TIME-CLEAR IT AND CHECK AGAIN
		JRST AGN]
	JRST NOVRID+2	;AND THIS IS NOT RESTR TIME, SO CHECK USERS
NOVRID:	TRNN F,DUREST	;IS THIS DURING RESTR. TIME?
	JRST RESOFF	;NO, MAKE SURE IT'S NOT RESTR. AND RETURN
	PUSHJ P,RESON	;YES, MAKE SURE SYSTEM IS RESTRICTED
	PUSHJ P,CREDIT	;CREDIT USER'S IF THIS IS THE FIRST RUN
	PUSHJ P,CHKHNG	;CHECK ON JOBS TO BE HUNG
RET:	RELEASE DSK,
	POPJ P,0

RESON:	MOVE A,[XWD 17,11]
	GETTAB A,
	 JRST BADGTT
	TROE A,100000
	JRST .+4
	HRLI A,1
	SETUUO A,	;SET RESTRICT
	 JRST UUOERR
	POPJ P,0

SETFLG:	MOVE A,TODAY
	MOVEM A,DATLOC
	TIMER A,
	MOVEM A,DATLOC+1
	HRRZI A,400000
	MOVEM A,DATLOC+2
	MOVEI A,DATLOC
	DATUUO A,
	 JRST UUOERR
	MOVE A,DATLOC
	IDIVI A,7
	CAIE B,3	;SATURDAY?
	CAIN B,4	;OR SUNDAY?
	JRST NOTSET	;YES,ITS A WEEKEND, SO NOT RESTRICTED TIME
	MSTIME A,
	IDIVI A,^D60000
	MOVE B,STRTRU
	CAMN B,STPTRU	;IF START=STOP, FORGET IT
	JRST NOTSET
	CAML A,B
	CAML A,STPTRU
NOTSET:	TRZA F,DUREST
	TRO F,DUREST
	POPJ P,0

RESOFF:	MOVE A,[XWD 17,11]
	GETTAB A,
	 JRST BADGTT
	TRZN A,100000
	JRST RET	;RETURN TO PJ
	HRLI A,1
	SETUUO A,
	JRST UUOERR
	JRST RET

;IF TOTALS HAVE NOT BEEN CLEARED TODAY - CLEAR THEM
CHKCLR:	HRRZ A,CLRTIM	;GET DATE TOTALS WERE LAST CLEARED
	CAMN A,TODAY	;CLEARED TODAY?
	POPJ P,0	;YES, OK
	PUSHJ P,CLRTOT	;NO, CLEAR THEM
	PUSHJ P,LOKCTL	;UPDATE CLRTIM IN CONTROL FILE
	PUSHJ P,ENTCTL	;OPEN FILE FOR UPDATING
		MOVE A,TODAY
	HRRZM A,BLOCK1+3	;SAVE DATE
	MSTIME A,
	IDIVI A,^D60000
	HRLM A,BLOCK1+3	;SAVE TIME
WRCTL:	USETO DSK,1
	OUTPUT DSK,BL1LST
	STATZ DSK,760000
	MAKERR 11,JOB,@THISYS,NXTJ
	CLOSE DSK,
	POPJ P,0



;READ THRU LUD AND CLEAR TOTAL FOR ANY USER WITH TRU CONTROL BIT ON.
CLRTOT:	PUSHJ P,LOKLUD
	PUSHJ P,SETJA
	SETZ N2,
NXTBLK:	INPUT DSK,BL1LST	;READ NEXT BLOCK
	STATZ DSK,20000
	JRST CLRDON	;END OF FILE FOUND
	STATZ DSK,740000
	JRST IOERR
	SETZ INDEX,
NXTU:	SKIPG A,BLOCK1(INDEX)	;CHECK NEXT USER
	JRST NXTBLK	;DONE WITH THIS BLOCK
	LDB SIZENT,SIZPTR
	SKIPN BLOCK1+4(INDEX)	;IIF NOT VALID
	JRST INCDEX	;DON'T CHECK IT
	LDB B,CBTPTR	;GET TRU CONTROL BIT
	SKIPN ,B
	JRST INCDEX	;IF THIS USER DOES NOT HAVE A BUDGET, GO TO NEXT
	PUSHJ P,CHGUFD	;SET ZERO IN UFD
INCDEX:	ADD INDEX,SIZENT
	JRST NXTU

;SET JACCT STATUS
SETJA:	HRROI A,JBTSTS
	GETTAB A,
	 JRST BADGTT
	TLO A,JACCT
	SETJAL A,
UUOERR:	MAKERR 1,JOB,@THISYS,NORET
	POPJ P,0

CLRDON:	CLOSE DSK,
	PUSHJ P,UNSETJ	;RESET JACCT BIT
	POPJ P,0

UNSETJ:	HRROI A,JBTSTS
	GETTAB A,
	 JRST BADGTT
	TLZ A,JACCT
	SETJAL A,
	 JRST UUOERR
	INCHRS A
	SKIPA
	EXIT
	POPJ P,0


;N2 CONTAINS THE NUMBER OF TRUS TO PUT IN UFD ENTRY
;A CONTAINS PPN
CHGUFD:	MOVE N,A
	PUSHJ P,SETRDX	;SET STRDEX UP FOR GETTING STR
NXTSTR:	MOVE STR,BLOCK1(STRDEX)
	AND STR,[XWD 777777,770000]
	PUSHJ P,LOKSTR	;GET THE INTERLOCK
	MOVEI A,17
	HRLI A,400000
	MOVEI C,0
	MOVE B,STR
	OPEN UFD,A
	JRST [PUSHJ P,NLKSTR
		JRST INIERR]
	PUSHJ P,LKUFST
	LOOKUP UFD,UFDFIL
	 JRST NOUFD1
	TRNE F,ADDIT
	JRST .+3
	MOVEM N2,UFDFIL+31
	SKIPA
	ADDM N2,UFDFIL+31
	SETOM UFDFIL+25	;SET IN USE WORD
	RENAME UFD,UFDFIL
	 JFCL
NOUFD1:	PUSHJ P,NLKSTR
	CLOSE UFD,
	RELEASE UFD,
	TRNE F,ONESTR
	JRST UFDDON
	JUMPN N2,UFDDON
	ADDI STRDEX,3
	MOVE A,INDEX
	ADD A,SIZENT
	CAMGE STRDEX,A
	JRST NXTSTR
UFDDON:	POPJ P,0

;CREDIT ALL BUDGETED USERS LOGGED IN WITH THE AMOUNT OF TRU'S USED SO FAR
CREDIT:	MOVE B,TODAY
	CAMN B,FIRSTR
	POPJ P,0
	PUSHJ P,ISEARCH
	PUSHJ P,SEARCH
	JRST SETCRE
	JRST .+5	;ALLREADY IN TABLE
	AOS USRDEX	;NEW USER
	MOVE N3,USRDEX
	MOVE N1,BLOCK1(INDEX)
	MOVEM N1,USRTAB(N3)	;SAVE PPN
	HRRZI N1,4
	HRL N1,JOBN
	GETTAB N1,	;GET TRU'S
	 MOVEI N1,0
	ADDM N1,CURTAB(N3)	;SAVE CURRENT TRU'S
	SOJG JOBN,CREDIT+4
SETCRE:	MOVE N3,USRDEX
	PUSHJ P,SETJA
	JUMPE N3,.+10
	TRO F,ONESTR+ADDIT
	MOVE A,USRTAB(N3)
	MOVN N2,CURTAB(N3)
	MOVE N,A
	MOVE STR,[SIXBIT /DSKB/]
	PUSHJ P,CHGUFD+4
	SOJG N3,.-5
	TRZ F,ONESTR+ADDIT
	PUSHJ P,UNSETJ
	PUSHJ P,LOKCTL
	PUSHJ P,ENTCTL
	MOVE A,TODAY
	MOVEM A,BLOCK1+5
	JRST WRCTL


CLROVR:	PUSHJ P,LOKCTL
	PUSHJ P,ENTCTL
	SETZM BLOCK1+4
	SETZM OVRIDE
	JRST WRCTL

ISEARCH: MOVE A,[XWD USRTAB,USRTAB+1]
	SETZM USRTAB
	BLT A,CURTAB+MAXUSR
	SETZM USRDEX
	MOVE JOBN,[XWD 20,12]
	GETTAB JOBN,
BADGTT:	MAKERR 22,JOB,@THISYS,NXTJ
	POPJ P,0

;SEARCH THRU USERS LOGGED IN WHO HAVE BUDGETS
SEARCH:	MOVEI N,6
	HRL N,JOBN
	GETTAB N,	;LOOK UP JBTPRV WORD FOR JOB
	 JRST BADGTT
	TRNN N,2000
	JRST CHKN	;USER DOES NOT HAVE A BUDGET
	MOVNI N,22
	HRL N,JOBN
	GETTAB N,	;GET FIRST HALF OF USR NAME
	JRST BADGTT
	JUMPE N,CHKN
	MOVNI N1,21
	HRL N1,JOBN
	GETTAB N1,
	JRST BADGTT
	PUSHJ P,FNDUSR	;NAME IN N,N1
	JRST CHKN	;USER NOT IN LUD???????
	LDB B,CBTPTR	;GET CONTROL BIT
	JUMPE B,CHKN	;IF BUDGET NOT SET, SKIP USER
	AOS (P)
	MOVE N3,USRDEX	;GET INDEX INTO USER TABLE
	HRRZ N1,BLOCK1(INDEX)	;GET UUN OF USER
	HRRZ A,USRTAB(N3)
	CAMN A,N1
	POPJ P,0
	SOJG N3,.-3
	AOS (P)
	POPJ P,0
CHKN:	SOJG JOBN,SEARCH
	POPJ P,0

;CHECK EACH USER LOGGED IN
;IF THE CURRENT TRU'S + TOTAL > BUDGET, HANG USER
CHKHNG:	PUSHJ P,ISEARCH
	PUSH P,JOBN
NXTJOB:	PUSHJ P,SEARCH
	JRST NXTCHK-2	;NO MORE JOBS
	JRST ADDTRU	;ALLREADY IN TABLE
	AOS USRDEX	;NEW USER
	MOVE N3,USRDEX
	MOVE N,BLOCK1+5(INDEX)
	MOVEM N,BUDTAB(N3)
	HRRZM N1,USRTAB(N3)
	MOVE N,BLOCK1(INDEX)	;GET PPN
	PUSHJ P,SETRDX
	MOVE STR,BLOCK1(STRDEX)
	AND STR,[XWD 777777,770000]
	MOVEM STR,.+2
	INIT UFD,17
	0
	0
	JRST INIERR
	PUSHJ P,LOKUFD
	MOVE C,UFDFIL+31	;GET TOTAL FOR DAY
	MOVEM C,CURTAB(N3)
ADDTRU:	HRRZI N1,4
	HRL N1,JOBN
	GETTAB N1,	;GET CURRENT TRU'S FOR JOB
	 JRST BADGTT
	ADDM N1,CURTAB(N3)
	SOJG JOBN,NXTJOB
	RELEASE UFD,
	POP P,JOBN
NXTCHK:	MOVNI N,23
	HRL  N,JOBN
	GETTAB N,	;GET GAN,UUN FOR JOB
	 JRST BADGTT
	HRRZ A,N
	MOVE N3,USRDEX
	HRRZ N,USRTAB(N3)
	CAME N,A	;FIND UUN IN TABLE
	JRST NUSR
	MOVE N,CURTAB(N3)
	CAMGE N,BUDTAB(N3)	;SHOULD JOB BE HUNG?
	JRST NXTJ1
;	MOVNI A,27
;	HRL A,JOBN	;SEE IF JOB ALLREADY DETACHED
;	GETTAB A,
;	JRST BADGTT
;	HLRZS A
;	JUMPN A,HNGDET+1	;JOB IS NOT DETACHED, SEND A DISCONNECT
;	HRLZ A,JOBN
;	GETTAB A,	;GET JOB STATUS WORD
;	 JRST BADGTT
;	TLNE A,400000	;COMMAND WAIT
;	JRST HNGDET	;YES, KILL JOB
;	TLNN A,200000	;IF JOB NOT IN ^C STATE WHILE DETACHED,
;	TRNE A,20000
;	JRST HNGDET	;KILL IT!
;	JUMPGE A,CHKN	;^C STATE - LET IT BE
HNGDET:	PUSHJ P,HANGIT
	PUSHJ P,DISCIT
NXTJ1:	SOJG JOBN,NXTCHK
	POPJ P,0
NUSR:	SOJG N3,NXTCHK+6
	JRST NXTJ1

;LOOKUP CONTROL FILE
LOKCTL:	PUSHJ P,SETUPC
	LOOKUP DSK,LK1FIL
LOKERR:	MAKERR 10,JOB,@THISYS,NXTJ
	INPUT DSK,BL1LST
	STATZ DSK,760000
IOERR:	MAKERR 11,JOB,@THISYS,NXTJ
	MOVE A,BLOCK1
	CAME A,[XWD 525252,104]
	MAKERR 21,JOB,@THISYS,NXTJ
	MOVE A,BLOCK1+1
	IDIVI A,^D100
	IMULI A,^D60
	ADD A,B
	MOVEM A,STRTRU	;START TIME
	MOVE A,BLOCK1+2
	IDIVI A,^D100
	IMULI A,^D60
	ADD A,B
	MOVEM A,STPTRU	;STOP TIME
	MOVE A,BLOCK1+3
	MOVEM A,CLRTIM	;DATE & TIME TOTALS WERE LAST CLEARED
	MOVE A,BLOCK1+4
	MOVEM A,OVRIDE	;OVERRIDE
	MOVE A,BLOCK1+5
	MOVEM A,FIRSTR	;TIME FOR FIRST RUN TODAY
	POPJ P,0

;ENTER CONTROL FILE
ENTCTL:	MOVEI B,^D10
	PUSHJ P,SETUPC
	ENTER DSK,LK1FIL
	SKIPA
	POPJ P,0
	HRRZ A,LK1FIL+3
	CAIE A,3	;IF NOT BUSY THIS IS A REGULAR
	JRST LOKERR	;!!ERROR!!
	MOVEI C,^D10	;DO A 10 SEC.
	SLEEP C,	;SLEEP
	SOJG B,ENTCTL+1	;AND TRY AGAIN
	JRST LOKERR	;TRIED 10 TIMES...GIVE UP!

;SET UP CONTROL FILE INFO
SETUPC:	MOVE A,[SIXBIT /LTCURT/]
	MOVEM A,LK1FIL
	HRLZI A,(SIXBIT /DAT/)
	MOVEM A,LK1FIL+1
	SETZM LK1FIL+2
	MOVE A,PJPPN
	MOVEM A,LK1FIL+3
	POPJ P,0


;LOOKUP LUD
LOKLUD:	HRLZI A,(SIXBIT /LUD/)
	MOVEM A,LK1FIL
	HRLZI A,(SIXBIT /SYS/)
	MOVEM A,LK1FIL+1
	SETZM A,LK1FIL+2
	MOVE A,SYSPPN
	MOVEM A,LK1FIL+3
	LOOKUP DSK,LK1FIL
	JRST LOKERR
	POPJ P,0

;LOOKUP UFD FOR PPN IN N
LKUFST:	MOVEI A,32
	MOVEM A,UFDFIL
	MOVE A,UFDPPN
	MOVEM A,UFDFIL+1
	MOVEM N,UFDFIL+2
	HRLZI A,(SIXBIT/UFD/)
	MOVEM A,UFDFIL+3
	POPJ P,0

LOKUFD:	PUSHJ P,LKUFST
	LOOKUP UFD,UFDFIL
	SETZM UFDFIL+31
	POPJ P,0


FNDUSR:	PUSHJ P,HASHER
	MOVE C,N1
	PUSHJ P,LOKLUD
	USETI DSK,@N1
	INPUT DSK,BL1LST
	STATZ DSK,760000
	JRST IOERR
	SETZ INDEX,
FND1:	SKIPN B,BLOCK1(INDEX)
	POPJ P,0
	JUMPL B,[HRRZ N,B
		JRST FNDUSR+3]
	CAMN N,BLOCK1+4(INDEX)
	JRST [AOS (P)
		POPJ P,0]
	LDB B,SIZPTR
	ADD INDEX,B
	JRST FND1

DISCIT:	HRLZI A,1	;DO LIKE A DISCONNECT ON A JOB NUMBER
	HRR A,JOBN
	HANG A,
	 JFCL
	POPJ P,0

HANGIT:	HRLZI A,3
	PUSHJ P,DISCIT+1
	AOS (P)
	POPJ P,0

WTLOK:	SOJLE N1,[MAKERR 23,JOB,@THISYS,NORET]
	MOVEI A,1
	SLEEP A,
        JRST RET        ;DONT TRY AGAIN
LOKSTR:	MOVEI N1,^D500
	MOVEI B,6	;CODE TO LOCK IT
	MOVE C,STR
	MOVE D,N
	MOVE A,[XWD 3,B]
	STRUUO A,
	JRST WTLOK
	POPJ P,0

NLKSTR:	MOVEI B,7	;UNLOCK CODE
	MOVE D,N
	MOVE C,STR	;STR NAME
	MOVE A,[XWD 3,B]
	STRUUO A,
	 JFCL
	POPJ P,0

SETRDX:	MOVEI STRDEX,6
	ADD STRDEX,INDEX
	LDB A,[POINT 1,BLOCK1+2(INDEX),27]
	JUMPE A,.+2
	ADDI STRDEX,3
	POPJ P,0
HASHER:	MOVEI B,0
	MOVE C,[555555555555]
	MOVE D,[361275431652]
	MOVE E,[612754316523]
	JSR RND
	JSR RND
	JSR RND
	JSR RND
	XOR E,D
	MOVE N,E
	TLZ N,400000
	IDIVI N,^D887
	MOVE N,E
	XOR N,C
	ADDI N1,1
	POPJ P,0

RND:	0
	ADD D,N
	ROTC N,-22
	MOVEI A,5
RND1:	MOVE N2,C+1(B)
	MUL N2,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
	ADDM N3,D+1(B)
	AOJE B,RND2
	MOVNI B,1
	TRNE D,1
	SKIPL E
	MOVEI B,0
	EXCH C,E
RND2:	SOJG A,RND1
	JRST @RND
	POPJ P,0

CBTPTR:	POINT 1,BLOCK1+2(INDEX),4
SIZPTR:	POINT 7,BLOCK1+2(INDEX),35
PJPPN:	6000214
SYSPPN:	1000004
UFDPPN:	1000001
MAXUSR==100
USRTAB:	BLOCK MAXUSR
BUDTAB:	BLOCK MAXUSR
CURTAB:	BLOCK MAXUSR
USRDEX: 0
LK1FIL:	BLOCK 4
UFDFIL:	BLOCK 31
	END
 