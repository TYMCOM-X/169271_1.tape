define !  = "comment", !! = "comment";
!! title Program Slam ;
! *************************************************************************
*************************************+*************************************
*                                                                         *
*                                                                         *
*                                  Slam                                   *
*                                                                         *
*                 Slam a bunch of files into another file                 *
*            without leading, trailing or redundant formfeeds             *
*                                                                         *
*                                                                         *
*************************************+*************************************

  edit history
    25-Jun-86   v1.1(72)  kmd   Report the number of pages copied.
    19-Mar-86   v1.1(70)  kmd   VMLine first indicates EOF or BOF by
                                returning 0 in Brk.  Remove
                                "length(Str)" from the EOF
                                consideration.
    13-Mar-86   v1.1(67)  kmd   Improve check for help character.  Add
                                protection violation error to input
                                open.
    11-Mar-86   v1.1(63)  kmd   Make FileProc return when Out.Slot is
                                negative.  Clarify protection
                                violation error on Out.Slot open.
                                Refine Is.Wild.Card to only check Fn
                                and Ext fields.
    10-Mar-86   v1.1(60)  kmd   Add indirect files.
     6-Mar-86   v1.0(56)  kmd   Fix some error messages.
     5-Mar-86   v0.5(55)  kmd   Parse the <Destination.File>
                                specification, and indicate when an
                                input Filespec is unmatched.
     4-Mar-86   v0.1(54)  kmd   Add NoticeToQuit logic, as well as
                                Append.Switch logic.
    26-Feb-86   v0.1(52)  kmd   Initial changes to make Slam a different
                                concept.


         This program was derived from PUT.SAI, and shares the
         following history with it:

    25-Feb-86   v1.2(51)  kmd   Make all loops - including the hidden
                                loop inside the UFDscn call - obey the
                                escape condition.
    24-Feb-86   v1.2(50)  kmd   Support escape (= ^C or ESC) interrupts,
                                doing the correct thing while printing to
                                the device.
     9-Jan-86   v1.1(40)  kmd   Change call to MFDscn to call of UFDscn for
                                uninteresting filespecs (ie, no wildcards
                                in UFD).  This was occasioned due to the
                                fact that the user needs RF license to read
                                the MFD, and this blocks the former routine
                                from accessing even UFDs to which it has
                                access through other means (eg, RP license)
     4-Dec-85   v1.0(37)  kmd   Change a few output messages.
    25-Nov-85   v1.0(36)  kmd   Inject MFDSCN into the scheme, adding
                                vanilla flavored wildcarding to filespecs.
    23-Nov-85   v1.0(31)  kmd   Add "Printing..." message.
    21-Nov-85   v1.0(27)  kmd   Pick up filespecs from the command line, if
                                they are present, otherwise, prompt.
    15-Nov-85   v1.0(16)  kmd   Strip leading and redundant embedded
                                FFs from the output.  Restructure to
                                loop the prompt until done.
    21-Oct-85   v1.0(12)  kmd   Change over to VMFILE for input.
     7-Oct-85   v0.1(11)  kmd   Skip leading FFs.
    14-Aug-85   v0.1(6)   kmd   Adapt for printer output.
    12-Aug-85   v0.1(5)   kmd   Install copy logic
     9-Aug-85   v0.1(4)   kmd   Do file lookup calls.
     6-Aug-85   v0.1(4)   kmd   Add requires for the world.
     5-Aug-85   v0.1(1)   kmd   First Edit - just a skeleton in some
                                closet.


***************************************************************************

    Future developments outstanding:

        o  Interesting Filespecs

          -  correct wildcards in Fn and Ext fields

          -  wildcards in UFD field (must check for RF license (or RP
             inside same GAN))


**************************************************************************;

begin "Slam"
  require "(KLib)Sail.Hdr"     source!file;
  require "(KLib)Sys.Req"      source!file;
  require "(KLib)Util.Req"     source!file;
  require "(Klib)CmdLin.Req"   source!file;
  require "(Klib)VMFile.Req"   source!file;
  require "(Sailib)MFDscn.Req" source!file;


! ************************************************************************;

  define
    EscChn = 10,
    NtqChn = 11;

  boolean
    Append.Switch,
    Esc.Occured,
    File.Matched,
    Ntq.Occured,
    Proceed;

  integer
    Index,
    Old.SpecLength,
    Out.Slot,
    Page.Count;

  string
    Full.FileSpec,
    Out.FileSpec,
    UFD.Out.Str;


  record!class IF.Rec
  (
    integer
      Slot;
    record!pointer (IF.Rec)
      Prev
  );
  
  record!pointer (IF.Rec)
    Ptr;


! ************************************************************************;
!! title Rtn WantFile ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=                              Rtn WantFile                               =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  boolean procedure WantFile (reference integer Fn1, Ext1);
  begin "WantFile"
    Full.Filespec _ UFD.Out.Str & cv6str(Fn1) & "." & cv6str(Ext1);
    set(File.Matched);
    return(Proceed);
  end "WantFile";


! ===================================+=====================================
==========================================================================;
!! title Rtn FileProc ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=                              Rtn FileProc                               =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  procedure FileProc (reference integer Ch, Cnt, Brk1, Eof;
      integer array LkB);
  begin "FileProc"
    integer
      Brk,
      In.Slot,
      OldBrk,
      Tmp;
    string
      Str;
    define
      FWD      = 0;


    if (not Proceed) then
      return;

    if (Out.Slot = 0) then
    begin
      integer
        Mode;

      Mode _ if (Append.Switch) then VM$Update else VM$Write;

      if ((Out.Slot _ VMFile(Out.FileSpec, Mode)) <= 0) then
      begin
        if (rh(Out.Slot) = !erprt) then
          print("Protection failure on """, Out.FileSpec, """", crlf)
        else
          print("VMFile Failed (""", Out.FileSpec, """, ",
              xwdstr(Out.Slot), ")", crlf);

        clear(Proceed);
        return;
      end;

      if (Append.Switch) then
      begin
        VMMove(Out.Slot, -1);
        VMMove(Out.Slot);

          ! This little sleight of hand establishes the character position
          ! as the end of file.  Were it not done, the next write operation
          ! would begin at the beginning of the file.
          ;
      end;
    end;

    if ((In.Slot _ VMFile(Full.FileSpec, VM$Read)) <= 0) then
    begin
      if (rh(In.Slot) = !erprt) then
        print("Protection failure on """, Full.FileSpec, """", crlf)
      else
        print("VMFile Failed (""", Full.FileSpec, """, ",
            xwdstr(In.Slot), ")", crlf);

      return;
    end;

    print(cr, Out.FileSpec,
        (if (Append.Switch) then " << " else " < "),
        Full.FileSpec,
        blanks(Old.SpecLength-length(Full.FileSpec)));

    Old.SpecLength _ length(Full.FileSpec);

    do
    begin
      Str _ VMLine(In.Slot, Brk, FWD);
      Tmp _ VMGetC(In.Slot, VM$ECR);
    end
    until (Brk neq FF or length(Str));

    if (Tmp) then
      appnd(Str, Tmp);

    if (Brk) then
      appnd(Str, Brk);

    if (Brk = FF) then
      inc(Page.Count);

    VMText(Out.Slot, Str);

    while (Brk and Proceed) do
    begin
      OldBrk _ Brk;
      Str    _ VMLine(In.Slot, Brk, FWD);
      Tmp    _ VMGetC(In.Slot, VM$ECR);

      if (length(Str) or Brk neq FF or OldBrk neq FF) then
      begin
        if (Tmp) then
          appnd(Str, Tmp);

        if (Brk) then
          appnd(Str, Brk);

        if (Brk = FF) then
          inc(Page.Count);

        VMText(Out.Slot, Str);
      end;
    end;

    VMFree(In.Slot);

    if (OldBrk neq FF) then
    begin
      Str _ FF;
      VMText(Out.Slot, Str);          ! Jump to next page;
    end;
  end "FileProc";


! ===================================+=====================================
==========================================================================;
!! title Rtn WantUFD ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=                               Rtn WantUFD                               =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  boolean procedure WantUFD (reference integer Aun);
  begin "WantUFD"
    return(Proceed);
  end "WantUFD";


! ===================================+=====================================
==========================================================================;
!! title Rtn UFDProc ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=                               Rtn UFDProc                               =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  procedure UFDProc (reference integer Chan; integer array Lkb);
  begin "UFDProc"
    UFD.Out.Str _ "(" & cv6str(Lkb['27]) & cv6str(Lkb['30]) & ")";
  end "UFDProc";


! ===================================+=====================================
==========================================================================;
!! title Rtn EscSvc ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=                               Rtn EscSvc                                =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple procedure EscSvc;
  begin "EscSvc"
    set(Esc.Occured);
    clear(Proceed);
  end "EscSvc";


! ===================================+=====================================
==========================================================================;
!! title Rtn NtqSvc ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=                               Rtn NtqSvc                                =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple procedure NtqSvc;
  begin "NtqSvc"
    set(Ntq.Occured);
    clear(Proceed);
  end "NtqSvc";


! ===================================+=====================================
==========================================================================;
!! title Rtn Print.Usage ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=                             Rtn Print.Usage                             =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple procedure Print.Usage;
  begin "Print.Usage"
    print("Usage: SLAM <Destination.File> = <FileSpecList>", crlf);
  end "Print.Usage";


! ===================================+=====================================
==========================================================================;
!! title Rtn Print.Help ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=                             Rtn Print.Help                              =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple procedure Print.Help;
  begin "Print.Help"
    print("Help goes here");
  end "Print.Help";


! ===================================+=====================================
==========================================================================;
!! title Rtn New.IF.Record ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=                            Rtn New.IF.Record                            =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  record!pointer (IF.Rec) procedure New.IF.Record
      (record!pointer (IF.Rec) Ptr1);
  begin "New.IF.Record"
    record!pointer (IF.Rec)
      X;

    X _ new!record(IF.Rec);
    IF.Rec:Slot[X] _ 0;
    IF.Rec:Prev[X] _ Ptr1;
    return(X);
  end "New.IF.Record";


! ===================================+=====================================
==========================================================================;
!! title Rtn Get.Next.Fs ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=                             Rtn Get.Next.Fs                             =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  string procedure Get.Next.Fs;
  begin "Get.Next.Fs"
    string
      Str;

    Str _ null;

    while (not length(Str)) do
    begin
      if (Ptr = nullrd) then
      begin
        if (Index >= ArgC) then
          return(null)
        else
        begin
          Str _ ArgV[Index];
          inc(Index);
        end;
      end
      else
      begin
        integer
          Brk;

        Str _ VMLine(IF.Rec:Slot[Ptr], Brk);

        if (not (Str or Brk)) then
        begin
          VMFree(IF.Rec:Slot[Ptr]);
          Ptr _ IF.Rec:Prev[Ptr];
          continue;
        end;
      end;

      if (Str = "@") then
      begin
        lop(Str);
        Ptr _ New.IF.Record(Ptr);

        if ((IF.Rec:Slot[Ptr] _ VMFile(Str, VM$Read)) <= 0) then
        begin
          print("No such indirect file as """, Str, """", crlf);
          Ptr _ IF.Rec:Prev[Ptr];
        end;

        Str _ null;
      end;
    end;

    return(Str);
  end "Get.Next.Fs";


! ===================================+=====================================
==========================================================================;
!! title Mainline Code;
! ***********************************+*************************************
*                                                                         *
*                                                                         *
*			       Mainline Code				  *
*                                                                         *
*                                                                         *
*************************************+************************************;


  begin "Mainline Code"
    string
      FileSpec;

    !version(!ver('1, '1), !edit('74), !date(1-July-1986));
    do.banner("Slam - Throw some files together");

    print(crlf);
    Old.SpecLength _ 0;
    Out.Slot _ 0;
    set(Proceed);
    Page.Count _ 0;

    clear(Esc.Occured);
    intmap(tinass(-1, !iaesc, EscChn), EscSvc, 0);
    enable(newenb(EscChn));

    clear(Ntq.Occured);
    intmap(intass(!iantq, NtqChn), NtqSvc, 0);
    enable(newenb(NtqChn));

    if (ArgC <= 1) then
    begin
      Print.Usage;
      exit;
    end
    else
    begin
      if (Is.Wild.Spec(ArgV[1])) then
      begin
        print("No wildcard characters allowed in <Destination.File>",
            crlf);
        exit;
      end
      else
      begin
        string
          Tmp;

        if (Parse.Filespec(ArgV[1], Tmp, Tmp, Tmp, Tmp)) then
        begin
          integer
            I;
          string
            Str,
            Str1;

          for I _ 1 step 1 until ArgC-1 do
          begin
            if (Contains(ArgV[I], "?")) then
            begin
              Print.Help;
              exit;
            end;
          end;

          Str          _ null;
          Str1         _ ArgV[2];
          Out.FileSpec _ ArgV[1];

          while (length(Str1)) do
          begin
            integer
              C;

            if ((C _ lop(Str1)) = "=" or C = "_") then
              C _ "<";

            appnd(Str, C);
          end;

          if (Str neq "<") then
          begin
            Print.Usage;
            exit;
          end
          else
            set(Append.Switch, equ(Str, "<<"));

          if (ArgC <= 3) then
          begin
            print("No input file specification.");
            exit;
          end;
        end
        else
        begin
          print("Bad Filespec """, ArgV[1], """", crlf);
          exit;
        end;
      end;
    end;

    Ptr   _ null!record;
    Index _ 3;

    while (Proceed and length(FileSpec _ Get.Next.Fs)) do
    begin
      integer
        Ext,
        Fn,
        PPN;
      string
        ExtStr,
        FnStr,
        PPNStr,
        UnStr;


      if (Parse.Filespec(FileSpec, UnStr, FnStr, ExtStr, PPNStr)) then
      begin
        Fn  _ cvsix(FnStr);
        Ext _ cvsix(ExtStr[2 to inf]);

        if (length(UnStr)) then
          PPN _ Cv.Username(UnStr)
        else if (length(PPNStr)) then
          PPN _ cvo(PPNStr)
        else
          PPN _ Get.Gfd.PPN;

        UFD.Out.Str _ "(" & Cv.PPN(PPN) & ")";
      end
      else
      begin
        print("Bad Filespec """, FileSpec, """", crlf);
        continue;
      end;

      clear(File.Matched);
      UFDscn(PPN, Fn, Ext, -1, WantFile, FileProc);

      if (not File.Matched) then
        print(crlf, "No file matched filespec """, FileSpec, """", crlf);
    end;

    if (Ntq.Occured) then
      print("Notice to Quit received - please log out");

    if (Esc.Occured) then
      print(" - ABORTED");

    if (Page.Count) then
      print(crlf, "Pages copied: ", Page.Count, crlf);

    if (Out.Slot) then
      VMFree(Out.Slot);

    exit;
  end "Mainline Code";
end "Slam";


! ***********************************+*************************************
**************************************************************************;
    l@ 8Ø