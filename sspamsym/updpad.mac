TITLE UPDPAD - UPDATE ADDRESS AND NODE-PRINTER FILE

;REQUIRES WF LICENSE - INSTALLED IN SPOOL

LOC 137
1
RELOC

A=1
B=2
C=3
D=4
BP=5
BLK=6
CH=7
OUTAUX=12
INAUX=13
INDEX=14
CNT=16
P=17

SIZADR==^D300	;5 LINES OF 60 CHARS EACH.
NADWDS==SIZADR/6	;NO. OF WORDS IN ADDRESS ENTRY

;COMMANDS
UPADR==102
UPNODP==101
ENDIT==103

START:	MOVE P,[IOWD 20,PDL]
	RESET
	MOVEI A,214
	SETMOD A,	;SET NO ECHO
	HRROI OUTAUX,4
	AUXCAL OUTAUX,1
	AUXCAL OUTAUX,2
	AUXCAL OUTAUX,3
	AUXCAL OUTAUX,4
	HRROI INAUX,1
TRYAGN:	AUXCAL INAUX,CH
	JRST .-1
	CAIE CH,1
	JRST TRYAGN
	AUXCAL INAUX,CH
	JRST .-1
	CAIE CH,2
	JRST TRYAGN
	AUXCAL INAUX,CH
	JRST .-2
	CAIE CH,3
	JRST TRYAGN
	AUXCAL INAUX,CH
	JRST .-1
	CAIE CH,4
	JRST TRYAGN
	INIT 1,17
	SIXBIT /DSK/
	0
	JRST AWAY
	AUXCAL OUTAUX,1
GETCMD:	AUXCAL INAUX,CH
	JRST .-1
	CAIN CH,ENDIT
	JRST AWAY
	CAIN CH,UPADR
	JRST DOADR
	CAIE CH,UPNODP
	JRST BAD
DONODP:	AUXCAL INAUX,A	;GET NODE NUMBER
	JRST .-1
	AUXCAL INAUX,B
	JRST .-1
	LSH A,^D8
	ADD A,B
	MOVEM A,NODE
	AUXCAL INAUX,PNUM
	JRST .-1
CHGNDP:	MOVE A,[SIXBIT /NODPRI/]
	SETZB B,C
	SETZ D,
	LOOKUP 1,A
	JRST BAD
	SETZB C,D
	SETZ B,
	ENTER 1,A
	JRST BAD
	MOVE A,NODE
	IDIVI A,^D128
	ADDI A,1	;HASH
	USETI 1,@A
	MOVEM A,BLK
	INPUT 1,SYSLST
	STATZ 1,760000
	JRST BAD
	MOVE A,NODE
	HRRZM A,SYSBLK(B)	;SAVE NODE
	MOVE A,PNUM
	HRLM A,SYSBLK(B)	;SAVE PRINTER LOC.
GOOD:	USETO 1,@BLK
	OUTPUT 1,SYSLST
	STATZ 1,760000
	JRST BAD  
	CLOSE 1,
	AUXCAL OUTAUX,6
	JRST GETCMD

BAD:	AUXCAL OUTAUX,25
	JRST GETCMD
DOADR:	AUXCAL INAUX,PNUM
	JRST .-1
	MOVEI CNT,SIZADR
	MOVE BP,ADRPTR
	AUXCAL INAUX,CH
	JRST .-1
	IDPB CH,BP
	SOJG CNT,.-3
CHGADR:	MOVE A,[SIXBIT /PRIADR/]
	SETZB B,C
	SETZ D,
	LOOKUP 1,A
	 JRST BAD
	SETZB B,C
	SETZ D,
	ENTER 1,A
	 JRST BAD
	MOVE A,PNUM
	ADDI A,2
	IDIVI A,2
	USETI 1,@A
	MOVEM A,BLK
	INPUT 1,SYSLST
	STATZ 1,20000
	PUSHJ P,[SETSTS 1,17
		SETZM SYSBLK
		MOVE A,[XWD SYSBLK,SYSBLK+1]
		BLT A,SYSBLK+177
		POPJ P,0]
	STATZ 1,740000
	JRST BAD
	IMULI B,^D64	;INDEX INTO HALF BLOCK
	MOVE INDEX,B
	MOVE A,PNUM
	MOVEM A,SYSBLK(INDEX)
FNDADR:	AOJ INDEX,
	MOVE A,[XWD ADDR,SYSBLK]
	ADD A,INDEX
	HRRZ B,A
	ADDI B,NADWDS-1
	BLT A,@B
	JRST GOOD
AWAY:	RELEASE 1,
	MOVEI A,^D10
	SLEEP A,
	HRRZI A,AWYBLK
	RUN A,
	EXIT
AWYBLK:	SIXBIT /SYS/
	SIXBIT /LOGOUT/
	SIXBIT /SHR/
	0
	1,,4
	0
PDL:	BLOCK 21
SYSLST:	IOWD 200,SYSBLK
	0
SYSBLK:	BLOCK 200
NODE:	0
PNUM:	0
ADDR:	BLOCK NADWDS
ADRPTR:	POINT 6,ADDR
END START
