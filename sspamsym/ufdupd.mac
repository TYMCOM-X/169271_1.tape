TITLE UFDUPD - UPDATE UFD ROUTINE

EXTERN UFDBLK

ENTRY UFDUPD

;REQUIRES JL ST LICENSE


;VERSION 4



;AC'S
A=1
B=2
C=3
D=4
N1=7
PPN=10
STR=14
BUDGET=16
P=13

;I/O CHANNELS
UFD==10

JBTSTS==0
JACCT==1

UFDUPD:	MOVE P,PDLWD
	MOVE PPN,UFDBLK
	MOVE BUDGET,UFDBLK+1
	MOVE STR,UFDBLK+2
	HRROI A,JBTSTS
	GETTAB A,
	 JRST NOGOOD
	TLO A,JACCT
	SETJAL A,
	 JRST NOGOOD
	PUSHJ P,LOKSTR	;GET THE INTERLOCK
	MOVEI A,17
	HRLI A,400000
	MOVEI C,0
	MOVE B,STR
	OPEN UFD,A
	JRST NOUFD
	PUSHJ P,LKUFST
	LOOKUP UFD,UFDFIL
	 JRST DONE
	MOVEM BUDGET,UFDFIL+31
	SETOM UFDFIL+25	;SET IN USE WORD
	RENAME UFD,UFDFIL
	JRST NOGOOD
DONE:	PUSHJ P,LETGO
	SETZ A,
	JRST @17

LETGO:	PUSHJ P,NLKSTR
	RELEASE UFD,
	HRROI A,JBTSTS
	GETTAB A,
	 JRST NOGOOD
	TLZ A,JACCT
	SETJAL A,
	 JRST NOGOOD
	POPJ P,0

NOUFD1:	PUSHJ P,LETGO
	TTCALL 3,[ASCIZ/
UFD NOT FOUND.
/]
	SETO A,
	JRST @17

;LOOKUP UFD FOR PPN IN N
LKUFST:	MOVEI A,32
	MOVEM A,UFDFIL
	MOVE A,UFDPPN
	MOVEM A,UFDFIL+1
	MOVEM PPN,UFDFIL+2
	HRLZI A,(SIXBIT/UFD/)
	MOVEM A,UFDFIL+3
	POPJ P,0

WTLOK:	SOJLE N1,NOGOOD
	MOVEI A,1
	SLEEP A,
	SKIPA	;TRY AGAIN
LOKSTR:	MOVEI N1,^D500
	MOVEI B,6	;CODE TO LOCK IT
	MOVE C,STR
	MOVE D,PPN
	MOVE A,STRARG
	STRUUO A,
	JRST WTLOK
	POPJ P,0

NLKSTR:	MOVEI B,7	;UNLOCK CODE
	MOVE D,PPN
	MOVE C,STR	;STR NAME
	MOVE A,STRARG
	STRUUO A,
	 JFCL
	POPJ P,0


NOUFD: PUSHJ P,NLKSTR
NOGOOD:	TTCALL 3,BADMSG
	EXIT
BADMSG: ASCIZ/

UFD NOT UPDATED.
PLEASE CALL TYMSHARE QUALITY ASSURANCE DEPARTMENT.
/
PDLWD: IOWD 20,PDL
PDL: 	BLOCK 20
STRARG: XWD 3,B
UFDPPN:	1000001
UFDFIL: BLOCK 40
	END
