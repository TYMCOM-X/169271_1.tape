TITLE COPNET - COPY NETWORK ACCTG. FROM THE 7/32'S
SUBTTL J. MARCIN 5/25/76

;VERSION
LOC 137
36	;36 (9/14/79) SEND SLAVE CONFIRMATION OF DATA BEFORE DATA
	;ARRIVES TO TRY TO SPEED THINGS UP A BIT
	;35 (8/30/79) ON CIR ZAP, LIMIT NUMBER OF RETRIES
	;34 (8/28/79) FIX PROBLEM WITH EARLY CIRCUIT ZAP THAT LEFT
	;(PJ)NETACC.DAT OPEN
	;33 (8/21/79) FINE TUNING TRY TO SPEED THINGS UP.
	;32 (8/10/79) MODIFY FOR GATEWAYS
	;31 (8/7/79) ADD GREATEST AND LEAST ELAPSED TIME TO DIR FILE
	;30 (8/7/79) TRY TO REBUILD ON ZAPPED CIRCUIT
	;27 (4/2/79) FIX BUG IN DIR FILE BUSY - LABEL FOR LOOP ADDED
	;26 (11/17/78) 3 DIGIT CHAN NUMBERS
	;25 (6/6/78) SUP SENDS YELLOW BALL AFTER EACH BLOCK.
	;CHANGE COPNET TO TURN OFF END OF FILE FLAG
	;NOTIFY 732 BLOCK WAS OK BEFORE WRITING OUT TO DISK!!!!!!!!!!
	;DECREASE TIME TO WAIT FOR BLOCK I/O EACH TIME TO 3 SECONDS
	;24 (5/30/78) IF BLOCK TIME IS -1, OUTPUT 9999999999. TO DIR FILE
	;23 (5/23/78) ADD FIRST BLK TIME TO DIR FILE - REMOVE CHKSUM FILE
	;22 (5/12/78) SET 1 MIN. TIMEOUT BEFORE SHAKE
	;21 (4/12/78) EACH SUP HAS ITS OWN PROGRAM
	;20 (9/22/77) IF ERROR, DELETE CURRENT FILE IF NO DATA IN IT
	;17 (9/20/77) TRY TO FIX ERROR IN DIR
	;16 (8/9/77) MOVE CHECK FOR CHECKSUM=-2
	;15 (7/12/77) TRY AGAIN.
	;14 (7/12/77) FIX DIR ERROR!!! I THINK!!!
	;13 (7/6/77) TRACK DOWN DIRECTORY ERROR
	;12 (6/24/77)IF CHECKSUM IS -2, IGNORE IT, -1 START AT BEGINNING
	;GET RID OF 18 SPACES IN DIR FILE
	;COPY DATA FIRST FROM THE SUP THAT WE COPIED LAST!
	;11 (4/28/77) CLOSE DSK BEFORE DOERR
	;10 (4/27/77) IF DIR FILE BUSY, LOOP 10 TIMES
	;7 FIX DATE ROUTINE
	;6 FORGET MINSIZ IN CASE SUP MOVES OFF AND LEAVES A FEW BLOCKS
	;TO BE COPIED
	;5 WRITE OUT WHEN WE HAVE 'MAXSIZ' BLOCKS AND USE 'MINSIZ' TO
	;DETERMINE CHECK FOR MINIMUM FILE SIZE
	;4 DO NOT COPY LESS THAN 10 BLOCKS
RELOC
SUPRN==^D124

;AC'S
F=0		;FLAGS
A=1
B=2
C=3
D=4
INDEX=5         ;INDEX INTO ARRAYS
CHAN=6          ;AUXILLIARY CIRCUIT CHANNEL NUMBER
N=7             ;COUNTER
CHECKS=10       ;CHECKSUM
DINDEX=11       ;INDEX INTO DISK BLOCK INFORMATION
CH=12           ;CHARACTER TO BE INPUT OR OUTPUT
COUNT==13	;COUNT FOR OPENING DIR FILE
OUTCH==14	;AC FOR CIRCUIT OUTPUT
BLKS==15	;NO. OF BLOCKS INPUT SINCE LAST TIMEOUT INTERRUPT
PTR=16          ;POINTER
P=17            ;PUSH DOWN LIST POINTER

;FLAGS IN F
TTYIO==1	;ON IF OUTPUT IS TO TTY

;NEEDS AC, WF LICENSE

;I/O CHANNEL
DSK==1
TTY==2	;USED FOR AUX CIR.


MAXSIZ==^D100	;MAXIMUM NO. OF 732 BLOCKS TO WRITE IN 1 OUTPUT FILE
		;(1 732 BLOCK = 2 PDP-10 BLOCKS)
START:	SKIPA
	JRST [MOVEI F,TTYIO	;I/O IS TO TTY
		MSTIME A,
		MOVEM A,STIME
		JRST GO]
	MSTIME A,
	MOVEM A,STIME	;TIME INITIALIZATION
	MOVEI A,TRPTAB
	HRLI A,6	;DISABLE AND CLEAR INTERRUPT SYSTEM
	INTADR A,
	 JFCL
	SETZ A,
	SETTIM A,	;RESET TIMER
	 JFCL
	HRLI A,4
	HRRI A,3	;SET TIMER ON 3
	INTASS A,
	 JFCL
	MOVE A,[XWD 1,^D60]
	SETTIM A,	;SET TIMEOUT FOR 1 MINUTE
	 JFCL
	MOVSI A,440000
	INTENB A,	;ENABLE INTERRUPT 1
	 JFCL
	MOVEI A,TRPTAB
	HRLI A,2
	INTADR A,
	 JFCL
	SETZ F,		;I/O IS OVER CIRCUIT
	MOVEI A,214	;SET NO ECHO
	SETMOD A,
	SETO A,		;CHANNEL
	MOVEI B,1	;FIRST CHARACTER FOR HANDSHAKE
	MOVE P,PDL
	PUSHJ P,SHAKE	;DO HANDSHAKE
	MOVSI A,40000
	INTENB A,	;DISABLE TIMER
	 JFCL
GO:	RESET           ;INITIALIZE
        SETZ A,         ;DETACH THE JOB
	TRNN F,TTYIO
	ATTACH A,
	 JFCL
GO1:	MOVE P,PDL      ;SET UP PUSH DOWN LIST
	INIT DSK,17	;INIT DISK IN DUMP MODE
	SIXBIT /DSK/
	0
	JRST STOP
        PUSHJ P,RDACC   ;READ (PJ)NETACC.DAT
	CLOSE DSK,	;CLOSE THE CONTROL FILE
	PUSHJ P,FINSUP
	JUMPE INDEX,DOSUP
	MOVEI A,ACCBLK
	ADD A,INDEX
	HRLZS A
	HRRI A,ACCBLK
	BLT A,ACCBLK+ACCTIM
	MOVEI A,ZAPTRY		;RE-TRY ON CIRCUIT ZAPS ONLY THIS NUMBER OF TIMES
	MOVEM A,ZAPCNT
SUBTTL MAIN ROUTINE

DOSUP:	MOVE B,ACCBLK+ACCLBK	;PICK UP BLOCK NUMBER LAST TRANSMITTED
        MOVEM B,OBLOCK	 	;AND SAVE IT
	MOVEM B,BLOCK
	MOVE A,ACCBLK+ACCCHK	;GET CHECKSUM FOR LAST BLOCK
	MOVEM A,LCHECKS		;AND SAVE IT
	SETOM FBLOCK		;INITIALIZE COUNTS
	SETZM DISKT
	SETZM CIRTIM
	SETZM CTIME1
	SETZM CTIME2
	SETZM LBLOCK
	SETZM NFILBLKS
	PUSHJ P,GETCIR          ;BUILD A CIRCUIT TO THE 7/32
        PUSHJ P,OPOFIL          ;OPEN THE OUTPUT FILE
	PUSHJ P,SETBLK		;GET LAST BLOCK AND COMPARE CHECKSUMS
	MSTIME A,		;SAVE TIME FOR INITIALIZATION
	SUB A,STIME
	IDIVI A,^D1000		;IN SECONDS
	MOVEM A,SUTIME		;SAVE 'START UP' TIME
NXTBLK:	MSTIME  A,
	MOVEM A,STIME		;TIME BLOCK TRANSFER
        AUXCAL OUTCH,CHKOK      ;TELL 7/32 WE LIKE IT
	PUSHJ P,GETBLK          ;INPUT BLOCK FROM 7/32
	 JRST GOTLST            ;7/32 SAYS WE ARE DONE
	MOVE A,FILBLK		;GET FIRST WORD OF BLOCK (BLK TIME)
	SKIPG BLKTIM		;IF NOT PREVIOUSLY SAVED FOR THIS FILE,
	MOVEM A,BLKTIM		;SAVE IT
	MSTIME A,
	SUB A,STIME		;SAVE TIME FOR BLOCK TRANSFER AND CHECKSUM
	ADDM A,CIRTIM
	MSTIME A,
	MOVEM A,STIME		;TIME DISK WRITE
	PUSHJ P,WBLK		;WRITE OUT BLOCK
	MSTIME A,
	SUB A,STIME
	ADDM A,DISKT
	MOVE A,NFILBLKS		;GET THE NUMBER OF BLOCKS WE HAVE COPIED
	CAIGE A,MAXSIZ		;ENUF TO WRITE OUT?
        JRST NXTBLK             ;NO, GO GET NEXT BLOCK
	MSTIME A,
	MOVEM A,STIME
	PUSHJ P,ENDIT		;CLOSE FILE
	SETZM NFILBLKS		;CLEAR VARIABLES
	SETZM LBLOCK
	SETZM DISKT
	SETZM CIRTIM
	SETZM CTIME1
	SETZM CTIME2
	SETOM FBLOCK
	PUSHJ P,OPOFIL		;OPEN NEW OUTPUT FILE
	MSTIME A,
	SUB A,STIME
	IDIVI A,^D1000
	MOVEM A,SUTIME		;SAVE START UP TIME FOR THIS FILE
	JRST NXTBLK		;AND CONTINUE
;7/32 SAYS WE HAVE ALL BLOCKS UP TO WHERE SUP IS WRITING
GOTLST:	PUSHJ P,ZAPIT		;TERMINATE CIRCUIT
	PUSHJ P,ENDIT
	JRST QUIT

;ROUTINE TO WRITE OUT FILE IF THERE IS SOME DATA,
;UPDATE DIRECTORY FILE,
;UPDATE (PJ)NETACC.DAT
ENDIT:	PUSHJ P,CLSDSK	;CLOSE DSK, DELETE FILE IF NOTHING THERE
	PUSHJ P,ADDNAM	;PUT NAME IN DIRECTORY
	PUSHJ P,UPDACC	;UPDATE (PJ)NETACC.DAT
	POPJ P,0
SUBTTL BUILD CIRCUIT

GETCIR: PUSHJ P,SETLOG	;SET UP LOGIN STRING
        MOVEI CHAN,LOGSTR
        CREAUX CHAN,	;AND TRY TO BUILD CIRCUIT
         JRST NOCIR
        HRLZ OUTCH,CHAN             ;GET CHANNEL NUMBER
        HRRI OUTCH,CHOUTI           ;AND SET UP TO SEND CHARACTERS
        AUXCAL OUTCH,CR             ;SEND 2 CR'S
        AUXCAL OUTCH,CR
        MOVEI A,TRPTAB          ;DISABLE AND CLEAR INTERRUPTS
        HRLI A,6        
        INTADR A,
         JFCL
        HRRZ A,CHAN             ;SET UP CIRCUIT ZAP ON 1
        HRLI A,7001
        TINASS A,
         JFCL
        HRLI A,4                ;SET UP TIME OUT ON 2
        HRRI A,2
        INTASS A,
         JFCL
        HRRZ A,CHAN             ;ENABLE INTERRUPTS
        MOVSI A,760000
        INTENB A,
         JFCL
        MOVE A,TIMO             ;SET UP TIME OUT
        SETTIM A,
         JFCL
	SKIPN ACCBLK+ACCGAT
	JRST GETC1
	HRLZ A,CHAN
	HRRI A,CHIN
GETCOL:	AUXCAL A,CH
	 JRST .-1
	ANDI CH,77
	CAIE CH,":"	;WAIT FOR ':' FROM 'PLEASE LOG IN:' MSG
	 JRST GETCOL
	HLLZ B,A
	HRRI B,52	;AUXCAL STRING OUTPUT
	AUXCAL B,GATLOG
	AUXCAL OUTCH,CR
GETC1:	MOVE A,CHAN	;CHANNEL FOR HANDSHAKE
	MOVEI B,201	;FIRST CHARACTER FOR HANDSHAKE
	PUSHJ P,SHAKE	;DO HANDSHAKE
        AUXCAL A,CH     ;INPUT FIRST 8 BITS OF CURRENT BLOCK NUMBER
         JRST .-1
        AUXCAL A,B      ;INPUT SECOND HALF OF CURRENT BLOCK NUMBER
         JRST .-1
        LSH CH,8        ;PUT 2 HALFS TOGETHER
        ADD CH,B
        MOVEM CH,CURBLK ;AND SAVE IT
	PUSHJ P,INITTY
	HRLZ A,CHAN
        HRRI A,SETBIO   ;SET UP FOR BLOCK I/0
        AUXCAL A,       ;SET IT
         JRST BIOERR
	MOVEI A,ZAPTRY		;RE-TRY ON CIRCUIT ZAPS ONLY THIS NUMBER OF TIMES
	MOVEM A,ZAPCNT
        POPJ P,0
SUBTTL CIRCUIT ROUTINES

;A/ CHANNEL
;B/ STARTING CHARACTER FOR HANDSHAKE
SHAKE:	HRLZS A,A
	HRRI A,CHOUT
	MOVEI N,4
	PUSH P,B
	AUXCAL A,B
	AOS B
	SOJG N,.-2
	POP P,B
	MOVEI N,4
	HRRI A,CHIN
TRYAGN:	MOVE C,B
	AUXCAL A,CH
	 JRST .-1
	CAME CH,C
	 JRST TRYAGN
	AOS C
	SOJG N,TRYAGN+1
	POPJ P,0

;ZAP ALL AUXILLIARY CIRCUITS
ZAPIT:	MOVSI A,300000	;DISABLE INTERRUPTS FIRST
	INTENB A,
	 JFCL
	SETO A,
	ZAPCIR A,
	POPJ P,0

;INIT TTY # IN CHAN
INITTY:	MOVE PTR,[POINT 6,TTYNAM,17]
	MOVE A,CHAN	;GET CHANNEL NUMBER
	LSH A,-6	;FIRST DIGIT
	ANDI A,7
	ADDI A,20
	IDPB A,PTR
	MOVE A,CHAN
	LSH A,-3	;SECOND DIGIT
	ANDI A,7
	ADDI A,20
	IDPB A,PTR
	MOVE A,CHAN
	ANDI A,7	;LAST DIGIT
	ADDI A,20
	IDPB A,PTR
	INIT TTY,14
TTYNAM:	SIXBIT /TTY000/
	0
	JRST CIRERR
	POPJ P,0
SUBTTL INPUT A 7/32 DISK BLOCK OF INFORMATION

;TRY TO INPUT A BLOCK OF DATA FROM 7/32
; TRY 5 TIMES IF CHECKSUM DOES NOT MATCH
; RETURN - END OF TRANSMISSION
; RETURN+1 - SUCCESSFUL TRANSMISSION
GETBLK: MOVEI A,ERRTRY          ;NUMBER OF TIMES TO RESEND DATA
        MOVEM A,ERRCNT          ;INITIALIZE COUNTER
SAMBLK: SETZ CHECKS,            ;INITIALIZE CHECKSUM
        MOVEI D,^D60            ;TRY 60 TIMES FOR FIRST INPUT
	MOVEI A,SIZBIO*4	;NUMBER OF WORDS TO INPUT
	MOVEM A,AUXCNT
        HRLZ C,CHAN             ;GET AUXILLIARY CHANNEL NUMBER
        HRRI C,BLKIN            ;AND SET IT UP FOR INPUT
GETAGN:	MSTIME A,
	MOVEM A,STIME1
	AUXCAL C,AUXCNT         ;INPUT A BLOCK
         JRST WAITA              ;IF NO DATA, WAIT FOR A TIME
	SETSTS TTY,14		;TURN OFF END OF FILE BITS
	AOJ BLKS,		;INCREMENT NUMBER OF BLOCKS INPUT
        SKIPN AUXCNT            ;IF NO WORDS, ERROR
         JRST BIOERR
        MOVE B,AUXBLK           ;GET FIRST WORD OF DATA
	SETZB A,DINDEX          ;CLEAR A FOR SHIFT AND DINDEX
        LSHC A,8                ;GET FIRST BYTE
        CAIN A,LSTBLK           ;IF SIGNAL OF END,
        POPJ P,0                ;DONE
	CAIE A,NEWBLK		;IF NOT A NEW BLOCK
	JRST SUPERR		;ERROR
        SETZ A,                 ;GET BLOCK NUMBER
        LSHC A,^D24
	MOVEM A,BLOCK		;AND SAVE IT
        MOVEI N,SIZBIO-1        ;NUMBER OF WORDS TO WRITE
        MOVEI INDEX,AUXBLK+1    ;PLACE TO START COPYING FROM
        PUSHJ P,COPWDS          ;COMPUTE CHECKSUM, COPY WORDS TO WRITE TO DISK
	MSTIME A,
	SUB A,STIME1
	ADDM A,CTIME1
	MSTIME A,
	MOVEM A,STIME1
        AUXCAL C,AUXCNT         ;GET NEXT BLOCK FROM 7/32
	 JRST .-1
	AOJ BLKS,
        MOVEI N,SIZBIO          ;NUMBER OF WORDS TO WRITE
        MOVEI INDEX,AUXBLK      ;PLACE TO START COPYING FROM
        PUSHJ P,COPWDS
        AUXCAL C,AUXCNT         ;GET LAST BLOCK
	 JRST .-1
	AOJ BLKS,
        MOVEI N,SIZBIO-1        ;NUMBER OF WORDS TO WRITE
        MOVEI INDEX,AUXBLK      ;PLACE TO START COPYING FROM
        PUSHJ P,COPWDS          ;COPY THEM
	MSTIME A,
	SUB A,STIME1
	ADDM A,CTIME2
	MOVE A,AUXBLK+SIZBIO-1  ;GET 7/32 CHECKSUM
	LSH A,-4		;GET IT RIGHT JUSTIFIED
        CAME A,CHECKS           ;DOES IT MATCH OURS?
         JRST BADCHK            ;NO, TRY AGAIN
	MOVEM A,LCHECKS		;SAVE LAST GOOD CHECKSUM
        AOS (P)                 ;RETURN+1
        POPJ P,0
;FIND OUT WHERE WE SHOULD START...
;GET CHECKSUM OF LAST BLOCK WE RECIEVED AND SEE IF IT MATCHES.
;IF IT DOES NOT, ASK FOR ENTIRE FILE
SETBLK:	MOVE A,LCHECKS
	CAMN A,[-1]		;IF CHECKSUM=-1, START AT BEGINNING
	JRST SENSTA
	PUSHJ P,SENBLK		;TELL 7/32 WHICH BLOCK TO SEND FIRST
	PUSH P,LCHECKS		;SAVE CHECKSUM OF LAST BLOCK
	PUSHJ P,GETBLK		;INPUT THAT BLOCK 
	 JFCL			;CAN'T BE DONE NOW
	POP P,A			;GET LAST CHECKSUM
	CAMN A,[-2]		;IF -2 CHECKSUM, DON'T COMPARE LAST ONE
	JRST SETOK
	CAME A,CHECKS		;DOES OLD CHECKSUM MATCH NEW ONE?
	JRST SETALL		;NO
SETOK:	AUXCAL OUTCH,CHKOK	;YES, TELL 7/32 ITS OK AND RETURN TO GET NEXT
	POPJ P,0
SETALL:	AUXCAL OUTCH,SNDALL		;CHECKSUM DOES NOT MATCH, GET ALL BLOCKS
	POPJ P,0

;TELL 7/32 WE WANT TO START WITH BLOCK 0
SENSTA:	SKIPN CURBLK	;IF 7/32 IS ON BLOCK 0, ACCTG. HAS BEEN CLEARED
	JRST GOTLST	;SO SKIP THIS SUP FOR NOW
	SETZM BLOCK	;START WITH BLOCK ZERO
	PUSHJ P,SENBLK
	POPJ P,0


;WRITE OUT A BLOCK OF 7/32 DATA TO PDP-10 DISK
WBLK:	MOVE A,BLOCK		;GET THIS BLOCK NUMBER
	SKIPGE FBLOCK		;IF NEGATIVE, SAVE FIRST GOOD BLOCK
	JRST [MOVEM A,FBLOCK	;AND SAVE IT
		JRST .+1]
	MOVEM A,LBLOCK		;SAVE THIS BLOCK NUMBER
	OUTPUT DSK,FILLST	;WRITE IT OUT
	STATZ DSK,760000	;ANY ERRORS?
	 JRST FILERR		;YES
	AOS NFILBLKS		;INCREMENT NO. OF BLOCKS IN THE FILE
	POPJ P,0
SUBTTL MISCELLANEOUS ROUTINES FOR GETTING A 7/32 BLOCK OF DATA

;ROUTINE TO WAIT FOR 1 MINUTE BEFORE WE RECIEVE A NEW BLOCK
;TRY EVERY SECOND
WAITA:	MOVEI N,1	;1 SECONDS
        SLEEP N,
        SOJG D,GETAGN	;TRY AGAIN
         JRST BIOERR


;ROUTINE TO:
;       COMPUTE CHECKSUM
;       SHIFT WORDS TO BE RIGHT JUSTIFIED
;       COPY TO DISK BUFFER
;INDEX/ LOCATION TO START COPYING FROM
;N/ NUMBER OF WORDS TO PROCESS
COPWDS: MOVE A,0(INDEX)		;GET NEXT WORD
        LSH A,-4	        ;SHIFT RIGHT
        ADD CHECKS,A		;ADD TO CHECKSUM
        AND CHECKS,CHKMSK       ;BE SURE WE ONLY HAVE 32 BITS
        MOVEM A,FILBLK(DINDEX)  ;SAVE IN DISK BUFFER
        AOJ DINDEX,
        AOJ INDEX,
        SOJG N,COPWDS
        POPJ P,0


;ROUTINE TO ASK FOR BLOCK OVER AGAIN
BADCHK:	SOSG A,ERRCNT	;HAVE WE TRIED ENOUGH TIMES?
        JRST CHKERR     ;YES, TOO BAD
        PUSHJ P,SENBLK  ;TELL 7/32 WE WANT THIS BLOCK AGAN
        JRST SAMBLK     ;AND GO GET IT


;ROUTINE TO TELL 7/32 WHAT BLOCK TO START WITH
SENBLK:	AUXCAL OUTCH,STBLK  ;TELL 7/32 BLOCK NUMBER IS COMING
	MOVE A,OUTCH
        HRRI A,CHOUT    
        MOVE B,BLOCK    ;GET BLOCK NUMBER
        LSHC B,-8       ;GET FIRST 8 BITS
        AUXCAL A,B      ;AND SEND THEM
	SETZ B,
        LSHC B,8        ;GET NEXT 8 BITS
        AUXCAL A,B      ;AND SEND THEM
        POPJ P,0        ;RETURN
;ROUTINE TO SET UP LOGIN STRING
SETLOG:	MOVE A,ACCBLK+ACCGAT	;PICK UP GATEWAY DESCRIPTION
	JUMPN A,SETGAT		;IF IT IS A GATEWAY, GO SET UP STRING
	MOVEI A,SUPRN		;GET 7/32 NUMBER
        IDIVI A,^D10    	;GET LAST DIGIT
        ADDI B,NUMASC		;MAKE IT ASCII
        DPB B,SUPPTR		;SAVE IT IN NAME
        POPJ P,0

SETGAT:	MOVE C,[POINT 7,LOGSTR+1,20]	;SET UP PTR TO HOST NUMBER
	IDIVI A,^D10
	PUSH P,B	;SAVE LAST DIGIT
	IDIVI A,^D10
	ADDI A,NUMASC
	IDPB A,C
	ADDI B,NUMASC
	IDPB B,C
	POP P,A
	ADDI A,NUMASC
	IDPB A,C
	MOVEI A,SUPRN
	IDIVI A,^D10
	ADDI B,NUMASC
	DPB B,[POINT 7,GATLOG+2,6]
	POPJ P,0
SUBTTL OUTPUT FILE ROUTINES

;ROUTINE TO OPEN OUTPUT FILE
;FORMAT OF FILE IS: SSHHMM.DDD
;WHERE SS = 7/32 SYSTEM
;	HHMM = HOUR AND MINUTE OF THE DAY
;	DDD = DAY OF THE YEAR
OPOFIL:	SETOM BLKTIM		;SET FLAG FOR SAVING BLOCK TIME
	MOVE PTR,[POINT 6,FILLOK+2]     ; SET UP FILE NAME
        MOVEI A,SUPRN           ;FIRST 7/32 NUMBER
        IDIVI A,^D100           ;LAST 2 DIGITS ONLY
        MOVE A,B
        PUSHJ P,MAKDIG          ;MAKE IT SIXBIT AND ADD TO NAME
        MSTIME A,               ;GET DAY TIME
        IDIVI A,^D60000          ;IN MINUTES
	MOVEM A,SAVTIM		;SAVE TIME IN CASE WE HAVE TO INCREMENT
	MOVEM PTR,SAVPTR	;AND PTR
	PUSHJ P,MAKTIM		;PUT TIME IN FILE NAME
        PUSHJ P,GETDAY		;GET DAY OF THE YEAR
        IDIVI A,^D100		;GET FIRST DIGIT
        ADDI A,NUMSIX   	;MAKE IT SIXBIT
        IDPB A,PTR		;AND ADD TO NAME
        MOVE A,B		;GET NEXT 2 DIGITS OF DAY
        PUSHJ P,MAKDIG          ;MAKE THEM SIXBIT AND ADD TO NAME
        SETZM FILLOK+1          ;THIS USER
	LOOKUP DSK,FILLOK	;SEE IF FILE ALLREADY EXISTS
	 SKIPA			;NO, WE ARE OK
	PUSHJ P,FIXNOF		;YES, INCREMENT TIME BY A MINUTE
        ENTER DSK,FILLOK        ;CREATE THE FILE
         JRST FILERR
        SETZM NFILBLKS          ;KEEP TRACK OF THE NUMBER OF BLOCKS WRITTEN
        POPJ P,0

;ROUTINE TO MAKE SIXBIT CHARACTERS OUT OF A 2 DIGIT NUMBER
MAKDIG: IDIVI A,^D10
        ADDI A,NUMSIX
        IDPB A,PTR
        ADDI B,NUMSIX
        IDPB B,PTR
        POPJ P,0

;ROUTINE TO TAKE TIME IN MINUTES IN A AND PUT TIME IN FILE NAME
;PTR SET UP
MAKTIM:	IDIVI A,^D60		;GET TIME IN HOURS AND MINUTES
        PUSH P,B		;SAVE MINUTES
        PUSHJ P,MAKDIG          ;MAKE HOUR ASCII AND ADD TO NAME
        POP P,A	 	        ;GET MINUTES
        PUSHJ P,MAKDIG          ;MAKE IT SIXBIT AND ADD TO NAME
	POPJ P,0
;ROUTINE TO FIND A FILE NAME THAT DOES NOT EXIST
;BY INCREMENTING THE TIME BY A MINUTE ON EACH TRY
FIXNOF:	MOVE PTR,SAVPTR		;GET POINTER TO TIME IN FILE NAME
	AOS SAVTIM		;TRY NEXT MINUTE
	MOVE A,SAVTIM		;GET TIME WE JUST TRIED
	PUSHJ P,MAKTIM		;MAKE TIME OF DAY ASCII AND ADD TO FILE NAME
	LOOKUP DSK,FILLOK	;DOES FILE EXIST?
	POPJ P,0		;NO, GREAT
	JRST FIXNOF		;YES, KEEP GOING

;ROUTINE TO GET JULIAN DAY
GETDAY:	DATE A,
	IDIVI A,^D372	;GET YEARS SINCE 1964
	IMULI A,^D372
	SETZ B,
	HRRZI C,400000
	MOVEI D,A
	DATUUO D,
	 SETZ A,	;# OF DAYS SINCE 1/1/64
	MOVEM A,SVDAYS#
	DATE A,
	TIMER B,
	HRRZI C,400000
	MOVEI D,A
	DATUUO D,
	 SETZ A,
	SUB A,SVDAYS
	AOJ A,
        POPJ P,0

;ROUTINE TO ADD NAME TO DIRECTORY FOR AIS
ADDNAM:	SKIPN NFILBLKS
	POPJ P,0
	MOVEI COUNT,^D100
	CLOSE DSK,
ADDIT:	LOOKUP DSK,DIRLOK       ;LOOKUP DIRECTORY FILE
         JRST [HRRZ A,DIRLOK+3  ;GET ERROR CODE
                JUMPN A,DIRERR  ;NOT A FILE NOT FOUND
		SETZM DIRLOK+5
		CLOSE DSK,
                JRST .+1]
	HLLZS DIRLOK+3
	SETZM DIRLOK+4
        ENTER DSK,DIRLOK
	JRST [HRRZ A,DIRLOK+3	;GET ERROR CODE
		CAIE A,3	;BUSY?
		CAIN A,4	;OR CONFLICT?
		SKIPA
		JRST DIRERR	;NO, SOME OTHER ERROR
		MOVEI A,^D10	;SLEEP FOR 10 SECONDS
		SLEEP A,
		CLOSE DSK,
		SETZM DIRLOK+5
		SOJGE COUNT,ADDIT	;SO TRY 100 TIMES
		JRST DIRERR]
	SETZ INDEX,
        MOVE A,DIRLOK+5         ;GET NUMBER OF WORDS
        IDIVI A,SIZBLK          ;GET NUMBER OF BLOCKS
        MOVEM A,DIRLOC
	JUMPE A,[AOS DIRLOC	;IF EMPTY FILE, DON'T INPUT ANY
		PUSHJ P,ZERDIR	;ZERO DATA
		JRST CHKEND]
        USETI DSK,@A            ;SET CURSOR
        INPUT DSK,DIRLST        ;INPUT LAST BLOCK
	STATZ DSK,760000	;ANY ERRORS?
	JRST DIRERR
	STATZ 1,20000
	PUSHJ P,ZERDIR	;END OF FILE
CHKEND: SKIPN FILBLK(INDEX)     ;IF ZERO, FOUND END
        JRST FNDEND		;FOUND PLACE FOR NEW ENTRY
        ADDI INDEX,SIZDIR	;CHECK NEXT
	CAIGE INDEX,SIZBLK-1	;AT END OF BLOCK?
        JRST CHKEND
	PUSHJ P,DIRNEW		;YES, MAKE NEW BLOCK FOR THIS ENTRY
FNDEND:	MOVE B,[POINT 6,FILLOK+2]       ;SET UP POINTER TO GET FILE NAME
        MOVE PTR,[POINT 7,FILBLK(INDEX)] ;SET UP POINTER TO SAVE FILE NAME
        MOVEI N,6          ;6 CHARACTERS IN FILE NAME
        ILDB A,B                ;GET NEXT CHARACTER
        ADDI A,SIXASC           ;MAKE IT ASCII
        IDPB A,PTR
        SOJG N,.-3
        MOVEI A,"."             ;SAVE DOT BETWEEN FILE AND EXTENSION
        IDPB A,PTR
        MOVEI N,3               ;3 CHARACTER EXTENSION
        ILDB A,B	        ;GET NEXT CHARACTER
        ADDI A,SIXASC           ;MAKE IT ASCII
	IDPB A,PTR		;AND SAVE IT
	SOJG N,.-3
	MOVE A,FBLOCK		;WRITE OUT FIRST BLOCK IN THIS FILE
	PUSHJ P,ADDNUM
	MOVE A,LBLOCK		;WRITE OUT LAST BLOCK IN THIS FILE
	PUSHJ P,ADDNUM
	MOVE A,BLKTIM
	CAMN A,[XWD 37777,777777]
	MOVE A,[^D9999999999]
	PUSHJ P,ADDTIM		;WRITE OUT 10 DIGITS OF BLOCK TIME
	MOVE A,SUTIME		;OUTPUT START UP TIME
	PUSHJ P,ADDNUM		;IN 4 PLACES
	MOVE A,CIRTIM		;OUTPUT TRANSFER TIME
	IDIVI A,^D1000
	PUSHJ P,ADDNUM		;IN 4 PLACES
	MOVE A,DISKT
	IDIVI A,^D1000
	PUSHJ P,ADDNUM
	MOVE A,CTIME1
	IDIVI A,^D1000
	PUSHJ P,ADDNUM
	MOVE A,CTIME2
	IDIVI A,^D1000
	PUSHJ P,ADDNUM
        MOVEI A,CR              ;AND CR-LF
        IDPB A,PTR
        MOVEI A,LF
        IDPB A,PTR
        PUSHJ P,WRDIR
        CLOSE DSK,
        POPJ P,0
;ROUTINE TO WRITE OUT BLOCK AND INITIALIZE A NEW ONE
DIRNEW:	AOS DIRLOC
	PUSHJ P,ZERDIR		;ZERO DATA
	POPJ P,0

;ZERO BLOCK OF DATA FOR DIRECTORY
ZERDIR:	SETZB INDEX,FILBLK
        MOVE A,[XWD FILBLK,FILBLK+1]
        BLT A,FILBLK+SIZBLK-1
        POPJ P,0


;ROUTINE TO WRITE OUT A DISK BLOCK
WRDIR:  USETO DSK,@DIRLOC
        OUTPUT DSK,DIRLST
        STATZ DSK,760000
         JRST DIRERR
        POPJ P,0

;WRITE OUT THE NUMBER IN A AS 4 ASCII CHARACTERS
ADDNUM:	MOVEI N,4		;4 CHARACTERS
	MOVEI C,^D1000		;DIVISOR FOR FIRST DIGIT
ADDNXT:	IDIV	A,C		;GET NEXT DIGIT
	ADDI A,NUMASC		;MAKE IT ASCII
	IDPB A,PTR		;SAVE IT
	MOVE A,B		;GET REST OF NUMBER
	IDIVI C,^D10		;GET NEXT DIVISOR
	SOJG N,ADDNXT		;IF WE NEED MORE, GO GET THE NEXT ONE
	POPJ P,0


;WRITE OUT THE NUMBER AS 10 ASCII CHARACTERS
ADDTIM:	MOVEI N,^D10
	MOVE C,[^D1000000000]
	JRST ADDNXT
SUBTTL (PJ)NETACC.DAT ROUTINES

;ROUTINE TO UPDATE (PJ)NETACC.DAT
UPDACC:	SKIPN NFILBLKS
	 POPJ P,0	;DON'T UPDATE IF NO BLOCKS COPIED
	MOVEI COUNT,^D100
UPDAC1:	LOOKUP DSK,ACCLOK
	 JRST ACCERR
	ENTER DSK,ACCLOK	;IN UPDATE MODE
	JRST [HRRZ A,ACCLOK+3	;GET ERROR CODE
		CAIE A,3
		CAIN A,4
		SKIPA
		JRST ACCERR
		MOVEI A,^D10
		SLEEP A,
		CLOSE DSK,
		SETZM ACCLOK+5
		SOJGE COUNT,UPDAC1
		JRST ACCERR]
	PUSHJ P,INACC
	PUSHJ P,FINSUP
	MOVE A,LBLOCK		;GET BLOCK WE ARE WORKING ON
	MOVEM A,ACCBLK+ACCLBK(INDEX)	;SAVE BLOCK NUMBER
	MOVE A,LCHECKS
	MOVEM A,ACCBLK+ACCCHK(INDEX)	;SAVE CHECKSUM OF LAST GOOD BLOCK
	DATE A,
	HRLZM A,ACCBLK+ACCDAT(INDEX)	;SAVE DATE
	MSTIME A,
	IDIV A,[^D60000]		;GET TIME IN MINUTES
	HRRM A,ACCBLK+ACCTIM(INDEX)	;AND SAVE IT
	USETO DSK,1		;WRITE INTO FIRST BLOCK
	OUTPUT DSK,ACCLST	;WRITE OUT BLOCK
	STATZ DSK,760000	;ANY ERRORS?
	 JRST ACCERR		;YES
	CLOSE DSK,		;NO, CLOSE FILE
	POPJ P,0
;ROUTINE TO READ (PJ)NETACC.DAT
RDACC: LOOKUP DSK,ACCLOK       ;LOOKUP FILE
         JRST ACCERR
INACC:	INPUT DSK,ACCLST        ;INPUT DATA
        STATZ DSK,760000        ;ANY ERRORS?
         JRST ACCERR            ;YES
        POPJ P,0

;FIND SUPRN IN ACCBLK
FINSUP:	SETZ INDEX,
	SKIPN N,ACCBLK+ACCSUP(INDEX)
	 JRST QUIT	;NOT FOUND
	CAIN N,SUPRN
	POPJ P,0
	ADDI INDEX, SIZACC
	JRST FINSUP+1
SUBTTL ERROR ROUTINES

QUIT:	PUSHJ P,ZAPIT		;ZAP ALL CIRCUITS
	HRLZ A,CHAN		;GET CHANNEL
	HRRI A,ENDBIO		;AND RELEASE BIO
	AUXCAL A,
	JFCL
STOP:	RELEASE DSK,
	TRNE F,TTYIO
	EXIT
STOP1:	HRLZI A,1
	HRRI A,AWYBLK		;LOGOUT
	RUN A,
	EXIT			;SHOULD NOT FAIL

;ERROR IN (PJ)NETACC.DAT
ACCERR:	CLOSE DSK,
	MOVEI A,ERRDAT
	JRST DOERR

;CLOSE DISK AND DELETE EXISTING FILE IF IT IS EMPTY
CLSDSK:	SKIPE NFILBLKS	;IF NOTHING WRITTEN AS YET
	JRST .+3
	CLOSE DSK,40	;DELETE FILE
	POPJ P,0
	CLOSE DSK,	;ELSE NORMAL CLOSE
	POPJ P,0

;ERROR IN BLOCK I/O
BIOERR:	PUSHJ P,ENDIT	;CLOSE DSK, DELETE FILE IF NOTHING THERE

	MOVEI A,ERRBIO
	JRST DOERR

;TRIED TO GET A BLOCK 5 TIMES TO NO AVAIL
CHKERR:	PUSHJ P,ENDIT	;CLOSE DSK, DELETE FILE IF NOTHING THERE

	MOVEI A,ERRBAD
	JRST DOERR

;CIRCUIT ZAPPED
CIRZAP:	PUSHJ P,ENDIT	;CLOSE DSK, DELETE FILE IF NOTHING THERE
	MOVEI A,ERRZAP
	JRST DOERR

;ERROR IN UPDATING DIRECTORY FILE
DIRERR:	CLOSE DSK,
	MOVEI A,ERRDIR
	JRST DOERR

;ERROR IN OUTPUT FILE
FILERR:	PUSHJ P,CLSDSK	;CLOSE DSK, DELETE FILE IF NOTHING THERE
	MOVEI A,ERROUT
	JRST DOERR

;CANNOT BUILD A CIRCUIT
NOCIR:	HLRZ A,CHAN		;GET PDP-10 ERROR
	JUMPN A,CIRERR		;IF NOT 'SYS UNAVAILABLE' GIVE ERROR
	HRRZ A,CHAN		;GET SUP ERROR
	CAIN A,4		;IF NOT 'SYS UNAVAILABLE' GIVE ERROR
	JRST QUIT
CIRERR:	MOVEI A,ERRCIR
	JRST DOERR

;ERROR FROM SUPERVISOR
SUPERR:	MOVEI A,ERRSUP
	JRST DOERR


TIMEUP:	MOVEM A,SAVA	;SAVE AC
	JUMPE BLKS,TIMU1	;IF WE HAVE TAKEN SOME BLOCKS IN
	SETZ BLKS,	;ZERO COUNT
	MOVE A,TIMO	;RESET TIME OUT
	SETTIM A,
	 JFCL
	MOVE A,SAVA	;RESTORE AC
	DISMIS		;AND DISMISS
TIMU1:	MOVEI A,DOERR	;RETURN TO ERROR ROUTINE
	MOVEM A,TRPTAB+2
	MOVEI A,ERRTIM	;TIME OUT ERROR
	DISMIS

NOSHK:	MOVEI A,ERRTIM
	JRST DOERR
; A/ ERROR NUMBER
DOERR:	MOVEM A,COPERR		;SAVE ERROR
	LOOKUP DSK,ERRLOK
	 JRST [HRRZ A,ERRLOK+3
		JUMPN A,STOP
		JRST .+1]
	HLLZS DIRLOK+3
	SETZM DIRLOK+4
	ENTER DSK,ERRLOK
	 JRST STOP
	SETZ INDEX,
	MOVE A,ERRLOK+5		;GET NUMBER OF WORDS
	IDIVI A,SIZBLK		;GET NUMBER OF BLOCKS
	MOVEM A,ERRLOC		;SAVE LOCATION
	JUMPE A,[PUSHJ P,ERRNEW	;IF EMPTY, DON'T INPUT
		JRST ERREND]
	USETI DSK,@A		;SET CURSOR
	INPUT DSK,ERRLST	;INPUT LAST BLOCK
	STATZ DSK,760000	;ANY ERRORS?
	 JRST STOP		;YES, EXIT
ERREND:	SKIPN FILBLK(INDEX)	;IF ZERO, FOUND END
	JRST .+3
	ADDI INDEX,SIZERR	;CHECK NEXT
	JRST ERREND
	CAIL INDEX,SIZBLK	;IF BLOCK IS FULL
	PUSHJ P,ERRNEW		;MAKE A NEW BLOCK
	DATE A,			;GET DATE
	MOVEM A,FILBLK(INDEX)	;AND SAVE IT
	MSTIME A,		;GET TIME
	IDIVI A,^D60000		;IN MINUTES
	MOVEM A,FILBLK+1(INDEX)	;AND SAVE
	MOVE A,COPERR		;RETRIEVE ERROR NUMBER
	MOVEM A,FILBLK+2(INDEX)	;AND SAVE
	MOVEI A,SUPRN		;GET SUPERVISOR NUMBER
	MOVEM A,FILBLK+3(INDEX)	;AND SAVE
	USETO DSK,@ERRLOC	;OUTPUT TO CORRECT BLOCK
	OUTPUT DSK,ERRLST	;OUTPUT THE BLOCK
	STATZ DSK,760000	;ANY ERRORS?
	 JRST STOP		;YES, JUST EXIT
	CLOSE DSK,
	MOVE A,COPERR
	CAIE A,ERRZAP		;WAS ERROR ZAPPED CIRCUIT?
	JRST STOP1
	RELEASE DSK,		;YES, TRY TO REBUILD
	SOSGE ZAPCNT		;RETRIED ENOUGH?
	 JRST STOP1		;YES, EXIT AND LET PJ START IT UP AGAIN
	MOVE A,[XWD 1,^D60*5]	;WAIT 5 MINUTES
	HIBER A,
	 JFCL
	JRST GO1		;START OVER
;CREATE A NEW BLOCK FOR ERRORS
ERRNEW:	AOS ERRLOC		;MAKE IT THE NEXT BLOCK
	SETZB INDEX,FILBLK
	MOVE A,[XWD FILBLK,FILBLK+1]
	BLT A,FILBLK+SIZBLK-1	;ZERO THE BLOCK
	POPJ P,0		;AND RETURN
SUBTTL STORAGE

;DEFINITIONS
CR==15                  ;CARRIAGE RETURN
ERRTRY==1               ;NUMBER OF TIMES TO TRY TO GET A GOOD BLOCK
			;SINCE CONFIRMATION IS NOW (9/14/79) SENT
			;BEFORE BLOCK IS CHECKED, ON A CHECKSUM
			;ERROR, STOP!
LF==12                  ;LINE FEED
NUMASC==60              ;DIFFERENCE BETWEEN NUMERIC AND ASCII
NUMSIX==20              ;DIFFERENCE BETWEEN NUMERIC AND SIXBIT
PJPPN==6000214          ;PJ'S PPN
SIXASC==40              ;DIFFERENCE BETWEEN SIXBIT AND ASCII
SIZACC==5               ;SIZE OF PJ'S CONTROL ENTRY
SIZBIO==^D86		;SIZE OF A BLOCK OF BIO DATA
SIZDIR==8               ;SIZE OF DIRECTORY ENTRY
SIZERR==4		;SIZE OF ERROR ENTRY
SIZBLK==^D128           ;SIZE OF TYMCOM-X DISK BLOCK

; AUXCAL'S
CHIN==1                 ;INPUT A CHARACTER
CHOUT==3                ;OUTPUT A CHARACTER FROM STORAGE
CHOUTI==4               ;OUTPUT A CHARACTER IMMEDIATE
SETBIO==34              ;SET UP BLOCK I/O
BLKIN==36               ;INPUT A BLOCK OF DATA
ENDBIO==42		;TERMINATE BLOCK IO

;CODES FOR COMMUNICATION WITH 7/32
STBLK==205              ;START WITH THE BLOCK NUMBER THAT FOLLOWS
NEWBLK==206             ;HERE COMES A BLOCK
CHKOK==207              ;CHECKSUM OK, SEND NEXT BLOCK
LSTBLK==210             ;NO MORE BLOCKS FROM THE 7/32
SNDALL==211		;7/32 SHOULD SEND ALL ITS BLOCKS

;ERRORS
ERRDAT==1	;ERROR IN PJ'S CONTROL FILE
ERRBIO==2	;ERROR IN BLOCK I/O
ERRBAD==3	;BAD BLOCK - TRIED 5 TIMES
ERRTIM==4	;TIME OUT ON CIRCUIT
ERRZAP==5	;ZAPPED CIRCUIT
ERRCIR==6	;ERROR IN BUILDING CIRCUIT
ERROUT==7	;ERROR IN OUTPUT FILE
ERRDIR==10	;ERROR IN UPDATING DIRECTORY
ERRSUP==11	;ERROR FROM SUP

;INDEX INTO ACCBLK
ACCSUP==0		;SUPERVISOR NUMBER
ACCLBK==1		;LAST BLOCK TRANSMITTED
ACCCHK==2		;CHECKSUM OF LAST BLOCK TRANSMITTED
ACCDAT==3		;DATE WE LAST TRANSMITTED DATA
ACCTIM==3		;TIME WE LAST TRANSMITTED DATA
ACCGAT==4		;ZERO IF NOT A GATEWAY,
			;OTHERWISE, THE GATEWAY HOST
;LOOKUP AND I/O STORAGE
ACCLOK: 3               ;AREA FOR READING AND WRITING PJ'S CONTROL FILE
        PJPPN
        SIXBIT /NETACC/
        SIXBIT /DAT/
ACCBLK: BLOCK 110
ACCLST: IOWD 100,ACCBLK
        0

ERRLOK:	5		;ERROR FILE
	0
	SIXBIT /NETERR/
	SIXBIT /LOG/
	BLOCK 2
ERRLST:	IOWD 200,FILBLK
	0

FILLOK: 3               ;AREA FOR DATA FILE
        0
        BLOCK 2
FILBLK: BLOCK 400
FILLST: IOWD 400,FILBLK
        0

DIRLOK: 5               ;AREA FOR WRITING DIRECTORY FILE
	0
        SIXBIT /NETACC/
        SIXBIT /DIR/
        BLOCK 2
DIRLST: IOWD 200,FILBLK
        0

AWYBLK:	SIXBIT /SYS/
	SIXBIT /LOGOUT/
	0
	0
	XWD 1,4
	0
;CONTENTS OF ENTRY IN PJ'S CONTROL FILE
OBLOCK:  0               ;LAST BLOCK TRANSMITTED

;STORAGE

AUXCNT: SIZBIO*4	;AREA FOR READING BLOCK I/O - DO NOT
AUXBLK: BLOCK SIZBIO	;SEPARATE THESE 2 VARIABLES

BLKTIM:	0		;=-1 AFTER NEW FILE HAS BEEN OPENED
			;=BLOCK TIME AFTER FIRST BLOCK OF FILE TRANSFERRED
BLOCK:	0               ;BLOCK WE ARE CURRENTLY COPYING
COPERR:	0		;COPNET ERROR NUMBER
SUTIME:	0		;START UP TIME
CIRTIM:	0		;TIME SPENT COLLECTING AND CHECKING
CTIME1:	0
CTIME2:	0
DISKT:	0		;DISK WRITE TIME
CURBLK: 0               ;BLOCK SUPERVISOR IS CURRENTLY WRITING IN
ERRCNT: 0		;COUNT FOR TRYING A BLOCK OVER ERRTRY TIMES
DIRLOC: 0               ;BLOCK IN DIRECTORY WE ARE WRITING IN
ERRLOC:	0		;BLOCK IN ERROR FILE BEING WRITTEN IN
FBLOCK:	-1		;FIRST BLOCK WRITTEN IN FILE (SAVE BLOCK IF NEGATIVE)
LBLOCK:	0		;LAST BLOCK WRITTEN IN FILE
LCHECKS: 0		;LAST CHECKSUM
NFILBLKS: 0		;NUMBER OF BLOCK BEING WRITTEN OUT
SAVA:	0		;TO SAVE A DURING INTERRUPTS
SAVPTR:	0               ;SAVE POINTER TO TIME IN FILE NAME
SAVTIM:	0		;SAVE DAY TIME IN MINUTES
STIME:	0		;STARTING TIME
STIME1:	0
ZAPTRY==5		;NUMBER OF TIMES TO RE-TRY ON ZAPS
ZAPCNT:	0		;COUNT OF RE-TRIES ON ZAPPED CIRCUIT
CHKMSK:	XWD 37777,777777	;TO GET ONLY 32 BITS OF CHECKSUM
LOGSTR:	ASCIZ /ACCOUNT:12N;/	;LOGIN STRING
	0
GATLOG:	ASCIZ /ACCOUNT:124;TNUOCCAS;/	;GATEWAY LOGIN STRING
	0
SUPPTR:	POINT 7,LOGSTR+2,6	;POINTER TO DEPOSIT SUP NUMBER
TIMO:	XWD 1,^D360             ;6 MINUTES
PDL:    -17,,.+1                ;PUSH DOWN STORAGE
        BLOCK 17

TRPTAB: 0                       ;INTERRUPT TABLE
        CIRZAP
        0
        TIMEUP
	0
	NOSHK
        BLOCK ^D64

        END START
 <8så